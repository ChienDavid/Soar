#!/usr/bin/python
# Project: Soar <http://soar.googlecode.com>
# Author: Jonathan Voigt <voigtjr@gmail.com>
#
import os, sys, platform

Import('env', 'g_msvs_variant')
clone = env.Clone()

if 'g++' in env['CXX']:
        compiler = 'g++'
elif env['CXX'].endswith('cl') or (env['CXX'] == '$CC' and env['CC'].endswith('cl')):
        compiler = 'msvc'
else:
        compiler = os.path.split(env['CXX'])[1]

if compiler == 'g++':
        clone.Append(CPPFLAGS='-std=c++14')

clone.Prepend(CPPPATH = env.Dir('src').srcnode())
src = Glob('src/*.cpp')
t = env.Install('$OUT_DIR', clone.Program('UnitTests', src))

if 'MSVSProject' in env['BUILDERS']:
	proj = env.MSVSProject(
		target = '#unittests' + env['MSVSPROJECTSUFFIX'],
		srcs = [ str(s) for s in src ],
		buildtarget = t,
		variant = g_msvs_variant,
		auto_build_solution = 0,
	)
	Import('msvs_projs')
	msvs_projs.append(proj)

Return('t')
